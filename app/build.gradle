plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-parcelize'
    id 'kotlin-android'
    id 'jacoco'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([debugTree]))
    executionData.setFrom(fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code-coverage/connected/*coverage.ec"
    ]))
}

android {

    namespace 'com.latticeonfhir.android'
    compileSdk 33

    signingConfigs {
        release {
            storeFile file('../agni.jks')
            storePassword 'latticeagni'
            keyAlias 'key0'
            keyPassword 'latticeagni'
        }
    }

    defaultConfig {
        applicationId "com.latticeonfhir.android"
        minSdk 23
        targetSdk 33
        versionCode 6
        versionName "1.0.6"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        vectorDrawables {
            useSupportLibrary true
        }
    }

    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
                excludes = ['jdk.internal.*']
            }
        }
        unitTests.returnDefaultValues = true
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            testCoverageEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BASE_URL", "\"https://fhir.api.thelattice.org/fhir/api/v1/\""
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BASE_URL", "\"https://fhir.api.thelattice.org/fhir/api/v1/\""
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
        dataBinding true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.3'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    lintOptions {
        disable 'ParcelCreator'
    }
}

dependencies {

    //Android Core
    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'

    //Material Components
    implementation 'com.google.android.material:material:1.8.0'

    //Constraint Layout
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    //Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    testImplementation 'org.mockito:mockito-core:3.6.0'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"

    //Recycler View
    implementation 'androidx.recyclerview:recyclerview:1.3.0'

    //Hilt-Dagger
    implementation 'com.google.dagger:hilt-android:2.45'
    kapt 'com.google.dagger:hilt-compiler:2.45'

    //Encrypt Shared Preferences
    implementation 'androidx.security:security-crypto:1.0.0'

    //Room Database
    implementation 'androidx.room:room-runtime:2.5.1'
    implementation 'androidx.room:room-ktx:2.5.1'
    kapt 'androidx.room:room-compiler:2.5.1'

    //Room Database Test Helpers
    testImplementation 'androidx.room:room-testing:2.5.1'

    //Room Database Paging Support
    implementation 'androidx.room:room-paging:2.5.1'

    //Paging 3
    implementation 'androidx.paging:paging-runtime-ktx:3.1.1'

    //Jetpack Compose Paging
    implementation 'androidx.paging:paging-compose:1.0.0-alpha18'

    //Paging 3 Test Implementation
    testImplementation 'androidx.paging:paging-common-ktx:3.1.1'

    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.10.0'

    //GSON
    implementation 'com.google.code.gson:gson:2.10.1'

    //Fragment
    implementation 'androidx.fragment:fragment-ktx:1.5.7'

    //SQL Cipher
    implementation 'net.zetetic:android-database-sqlcipher:4.5.3'
    implementation 'androidx.sqlite:sqlite-ktx:2.3.1'

    //Timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    //compose
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.4.3"
    debugImplementation "androidx.compose.ui:ui-tooling:1.4.3"
    debugImplementation "androidx.compose.ui:ui-test-manifest:1.4.3"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.activity:activity-compose:1.7.1'
    implementation "androidx.compose.ui:ui:1.4.3"
    implementation "androidx.compose.ui:ui-tooling-preview:1.4.3"
    implementation 'androidx.compose.material3:material3:1.1.0-rc01'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.6.1'
    implementation 'androidx.compose.runtime:runtime-livedata:1.5.0-alpha03'

    // navigation
    implementation("androidx.navigation:navigation-compose:2.5.3")

    // date-time picker
    implementation("com.marosseleng.android:compose-material3-datetime-pickers:0.6.1")

    //Work Manager
    implementation 'androidx.work:work-runtime-ktx:2.8.1'
    //Work Manager Testing
    androidTestImplementation "androidx.work:work-testing:2.8.1"

    //Fuzzy Wuzzy
    implementation 'me.xdrop:fuzzywuzzy:1.4.0'

    // hilt- viewmodel compose
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    // Firebase Bom
    implementation platform('com.google.firebase:firebase-bom:31.5.0')

    // Firebase Crashlytics
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    // splash screen
    implementation 'androidx.core:core-splashscreen:1.0.0-beta02'

    // google consent api for auto detection of otp
    implementation 'com.google.android.gms:play-services-auth:20.5.0'
    implementation 'com.google.android.gms:play-services-auth-api-phone:18.0.1'
}

kapt {
    correctErrorTypes true
}